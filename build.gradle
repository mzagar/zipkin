subprojects {
    apply plugin: 'java'
    apply plugin: 'scala'

    ext {
        ////////////////////////////////
        // Commonly used dependencies //
        ////////////////////////////////

        mkDepGen = { genOne -> { String... names -> names.collect(genOne) } }
        finagle = mkDepGen { "com.twitter:finagle-${it}_${scalaInterfaceVersion}:6.26.0" }
        util = mkDepGen { "com.twitter:util-${it}_${scalaInterfaceVersion}:6.25.0" }
        scrooge = mkDepGen { "com.twitter:scrooge-${it}_${scalaInterfaceVersion}:3.19.0" }
        algebird = mkDepGen { "com.twitter:algebird-${it}_${scalaInterfaceVersion}:0.10.2" }
        zipkin = mkDepGen { project(":zipkin-${it}") }

        hbase = mkDepGen { name -> "org.apache.hbase:hbase${if (name) "-${name}" else ''}:0.98.13-hadoop2" }
        hbaseTest = { String... names -> hbase(*names) + hbase(*names).collect{"${it}:tests"} }

        hadoop = mkDepGen { "org.apache.hadoop:hadoop-${it}:2.4.0" }
        hadoopTest = { String... names -> hadoop(*names) + hadoop(*names).collect{"${it}:tests"} }

        twitterZookeeperVersions = [
                'candidate': "0.0.75",
                'group'    : "0.0.81",
                'client'   : "0.0.70",
                'server-set': "1.0.103"
        ]
        zk = mkDepGen { "com.twitter.common.zookeeper:${it}:${twitterZookeeperVersions[it]}" }

        twitterServer = "com.twitter:twitter-server_${scalaInterfaceVersion}:1.11.0"
        junit = 'junit:junit:4.12'
        slf4jLog4j12 = "org.slf4j:slf4j-log4j12:1.6.4"  // Dont forget to use for configuration 'runtime' only
        ostrich = "com.twitter:ostrich_${scalaInterfaceVersion}:9.9.0"
        griddle = 'com.yodle.griddle:griddle:1.7'

        scalaTestDeps = [
                "org.scalatest:scalatest_${scalaInterfaceVersion}:2.2.5",
                junit
        ]

        testDependencies = [
                junit,
                "org.scala-tools.testing:specs_${scalaInterfaceVersion}:1.6.9",
                "org.jmock:jmock:2.4.0",
                // jmock tests mock classes and require additional dependencies.
                "cglib:cglib:2.2.2",
                "asm:asm:1.5.3",
                "org.objenesis:objenesis:1.1"
        ]
    }

    //////////////////////////
    // Common configuration //
    //////////////////////////

    // SBT migration note: start of zipkinSettings
    group "io.zipkin"
    version zipkinVersion

    repositories {
        jcenter()
        maven { url "https://maven.twttr.com/" }
        maven { url "http://repo.typesafe.com/typesafe/releases/" }
    }

    dependencies {
        compile "org.scala-lang:scala-library:${scalaVersion}",
                // Was dependencyOverrides in SBT, will probably cause issues
                'org.apache.zookeeper:zookeeper:3.4.6', // internal twitter + kafka + curator
                'org.slf4j:slf4j-api:1.6.4' // libthrift 0.5 otherwise pins 1.5.x
                // EOF dependencyOverrides
    }
    // SBT migration note: end of zipkinSettings
}
